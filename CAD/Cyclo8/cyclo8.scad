use <util.scad>;
use <screw.scad>;
use <bearings.scad>;

//==============================================
R=25;
N=21;
e=1;
D=18;
//==============================================

//=================================================
function cycloid(R,N,e) = [
	let(r=R/N)
	let(A=360/N)
	let(n=A/.5)
	for (i=[0:N-1])
		let(a=i*A)
		for (j=[0:n-1])
			let (alpha=a+j*(A/n))
			let (theta=alpha*(R+r)/r)
			let(x=(R+r)*cos(alpha) - e*cos(theta))
			let(y=(R+r)*sin(alpha) - e*sin(theta))
			[x,y]
];
//=================================================

//=================================================
module cycloid_disk(h,R,N,e) {
	linear_extrude(h) {
		polygon(cycloid(R,N,e));
	}
}
//=================================================

//=================================================
module cycloid_wall(h,Rdisk,Ndisk,e,t) {
	r=R/N;
	N=Ndisk+1;
	R=Rdisk+r;
	Rx=R+2*r-2*e+t;
	difference() {
		cyl(h,Rx);
		tz(-1) {
			linear_extrude(h+2)
			rz(180) {
				polygon(cycloid(R,N,e));
				polygon(cycloid(R,N,e/2));
			}
		}
	}
}
//=================================================

//==============================================
module adapter() {
	difference() {
		t(-18,-18,0)
		minkowski(){
			box(36,36,5);
			cyl(1,3);
		}
		union() {
			tz(-1) cyl(7,15);
			t(-15.5,-15.5,-1) cyl(7,1.5);
			t(-15.5,+15.5,-1) cyl(7,1.5);
			t(+15.5,-15.5,-1) cyl(7,1.5);
			t(+15.5,+15.5,-1) cyl(7,1.5);
			t(-15.5,-15.5,2) cyl(5,4);
			t(-15.5,+15.5,2) cyl(5,4);
			t(+15.5,-15.5,2) cyl(5,4);
			t(+15.5,+15.5,2) cyl(5,4);		
		}
	}
}
//==============================================

//==============================================
module receiver_plate(D) {
	hcyl(7,D+6,D+4);
	difference() {
		hcyl(7,D-4,11);
		tz(-1)
		for (i=[0:5]) {
			rz(30+i*60)			
			tx(D)
			cyl(9, D-13);
		}
	}
	for (i=[0:5]) {
		rz(i*60)
		tx(D) {
			hcyl(2,4,1.5);
			hcyl(7,6,4);
		}
	}
	/*
	difference() {
		hcyl(7,26,11);
		for (i=[0:5]) {
			rz(i*60)
			tx(D) {
				tz(-1) cyl(9,1.5);
				tz(+3) cyl(5,  4);
			}
		}
	}
	*/
}
//==============================================

//==============================================
module receiver_bearing() {
	bearing_22x8x7();
}
//==============================================

//==============================================
module receiver_plate_assembly(D) {
	receiver_plate(D);
	receiver_bearing();
}
//==============================================

//==============================================
module receiver_pin() {
	screwM3x20();
}
//==============================================

//==============================================
module receiver_pin_bearing() {
	bearing_8x3x4();
}
//==============================================

//==============================================
module receiver_pin_spacer() {
	color("#000000")
	hcyl(1,3.5,1.5);
}
//==============================================

//==============================================
module receiver_pin_assembly() {
	receiver_pin();
	tz(6)  receiver_pin_spacer();
	tz(7)  receiver_pin_bearing();
	tz(11)  receiver_pin_spacer();
	tz(12)  receiver_pin_bearing();
	tz(16) receiver_pin_spacer();
	tz(20) nutM3();
}
//==============================================

//==============================================
module receiver_assembly(D) {
	
	tz(7)
	rx(180)
	receiver_plate_assembly(D);
	
	tz(1)
	for (i=[0:5]) {
		rz(i*60)
		tx(D)
		receiver_pin_assembly();
	}

	tz(18)
	receiver_plate_assembly(D);
	
}
//==============================================

//==============================================
module disk(R,N,e,D) {
	color("#00ff00") {
		difference() {
			//hcyl(4,30,11);
			cycloid_disk(4,R,N,e);
			for (i=[0:5]) {
				rz(i*60)
				tx(D)
				tz(-1)
				cyl(6,4+e);
			}
		}
	}
}
//==============================================

//==============================================
module disk_bearing() {
	color("#ff0000") hcyl(4,11,7.5);
}
//==============================================

//==============================================
module disk_assembly(R,N,e,D) {
	disk(R,N,e,D);
	disk_bearing();
}
//==============================================

//==============================================
module axis(e) {
	t(-10,-4,0) box(20,8,4);
	cyl(40,4);
	tx(-e) tz(35-18.5) cyl(5,7.5);
	tx(+e) tz(35-13.5) cyl(5,7.5);
}
//==============================================

//==============================================
receiver_assembly(D);

rz($t*360*10*(1/(N+1)) + (180/N))
tz(5)
cycloid_wall(21,R,N,e,3);

rz($t*360*10) {

	tz(-9)
	axis(e);
	
	tz(8)
	tx(-e)
	rz(-$t*360*10)
	color("#004000")
	disk_assembly(R,N,e,D);
	
	tz(13)
	tx(+e)
	rz(-$t*360*10)
	rz(180)
	disk_assembly(R,N,e,D);
	
}
//==============================================
